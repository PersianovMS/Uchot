#Область ПРОГРАММНЫЙ_ИНТЕРФЕЙС

//Возвращает выборку или массив данных всех распознаваний
Функция ВсеРаспознавания(Массивом = Ложь) Экспорт
	#Область ТЕКСТ_ЗАПРОСА
		Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Распознавания.Наименование КАК Наименование,
				|	Распознавания.Кошелёк КАК Кошелёк,
				|	Распознавания.Статья КАК Статья,
				|	Распознавания.КорКошелёк КАК КорКошелёк,
				|	Распознавания.УсловиеПрименения КАК УсловиеПрименения,
				|	Распознавания.ПравилоРаспознавания КАК ПравилоРаспознавания,
				|	Распознавания.Комментарий КАК Комментарий,
				|	Распознавания.Параметры.(
				|		ИмяПараметра КАК ИмяПараметра,
				|		ЗначениеПараметра КАК ЗначениеПараметра
				|	) КАК Параметры
				|ИЗ
				|	Справочник.Распознавания КАК Распознавания
				|ГДЕ
				|	НЕ Распознавания.ПометкаУдаления");
	#КонецОбласти
	ВыбРезультат = Запрос.Выполнить().Выбрать();
	Если Массивом Тогда
		Результат = Новый Массив;
		Пока ВыбРезультат.Следующий() Цикл
			ТекСтр = Новый Структура("Наименование,Кошелёк,Статья,КорКошелёк,УсловиеПрименения,ПравилоРаспознавания,Комментарий");
			ЗаполнитьЗначенияСвойств(ТекСтр, ВыбРезультат);
			СтрПараметры = Новый Структура;
			ПодстрокаВыборки = ВыбРезультат.Параметры.Выбрать();
			Пока ПодстрокаВыборки.Следующий() Цикл
				Если СтрПараметры.Свойство(ПодстрокаВыборки.ИмяПараметра) Тогда
					ТекМассив = ВОЛНА_ОбщегоНазначения_КлиентСервер.ПреобразоватьВМассив(СтрПараметры[ПодстрокаВыборки.ИмяПараметра]);
					ТекМассив.Добавить(ПодстрокаВыборки.ЗначениеПараметра);
					СтрПараметры.Вставить(ПодстрокаВыборки.ИмяПараметра, ТекМассив);
				Иначе
					СтрПараметры.Вставить(ПодстрокаВыборки.ИмяПараметра,ПодстрокаВыборки.ЗначениеПараметра);
				КонецЕсли;
			КонецЦикла;
			ТекСтр.Вставить("Параметры", Новый ФиксированнаяСтруктура(СтрПараметры));
			Результат.Добавить(Новый ФиксированнаяСтруктура(ТекСтр));
		КонецЦикла;
	Иначе
		Результат = ВыбРезультат;
	КонецЕсли;
	Возврат Результат
КонецФункции

#КонецОбласти

#Область СЛУЖЕБНЫЕ
	
#КонецОбласти
