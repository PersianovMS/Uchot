
#Область ПрограммныйИнтерфейс

// Добавляет в структуру СКД автогруппировку по умолчанию
// Параметры:
//  Корень  - Структура или группировка - Куда добавить группировку
//  Очистить - Булево - предварительно очистить. По умолчанию Ложь.
Процедура ДобавитьГруппировкуЛинейныйСписок(Корень, Очистить = Ложь) Экспорт
	Если Очистить Тогда
		Корень.Очистить();
	КонецЕсли;
	Группировка = Корень.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Группировка.Использование = Истина;			
	АвтоПоле = Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	АвтоПоле.Использование = Истина;
КонецПроцедуры // ДобавитьГруппировкуЛинейныйСписок

// Выгружает типизированные колонки НаборДанныхОбъекта
// Параметры:
//  СКД  - СхемаКомпоновкиДанных - Источник колонок
//  ИмяНабора  - Строка - Полное имя набора, колонки которого будут выгружены
// Возвращаемое значение:
//   ТаблицаЗначений   - с колонками как у заданного набора данных
Функция НаборДанныхОбъектВыгрузитьКолонки(СКД, ИмяНабора) Экспорт
	Результат = Новый ТаблицаЗначений;
	Набор = НайтиПолеСКДПоПолномуИмени(СКД.НаборыДанных, ИмяНабора);
	Если Набор <> Неопределено Тогда
		Для каждого Поле_ Из Набор.Поля Цикл
			Результат.Колонки.Добавить(Поле_.Поле, Поле_.ТипЗначения, Поле_.Заголовок);
		КонецЦикла;
	КонецЕсли;
	Возврат Результат
КонецФункции // НаборДанныхОбъектВыгрузитьКолонки

#Область МетодыИВыраженияСистемыКомпоновкиДанных

// Находит среди элементов полей СКД поле по полному имени
//
// Параметры:
//	Элементы - поля СКД
//	ПолноеИмя - полное имя поля СКД
//
// Возвращаемое значение:
//	Поле - поле СКД
//
Функция НайтиПолеСКДПоПолномуИмени(Элементы, ПолноеИмя) Экспорт

	масЧастейИмен = ИзПолногоИмениПоляПолучитьЧасти(ПолноеИмя);
	колЧастей = масЧастейИмен.Количество();
	
	текИмя = масЧастейИмен[0];
	Поле = НайтиПолеСКДПоИмени(Элементы, текИмя);
	Если Поле = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Сч = 2 По колЧастей Цикл
		текИмя = масЧастейИмен[Сч - 1];//текИмя +"." +
		Поле = НайтиПолеСКДПоИмени(Поле.Элементы, текИмя);
		Если Поле = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Поле;

КонецФункции


#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает полное имя поля по частям
//
// Параметры:
//	ПолноеИмя - полное имя поля СКД
//
// Возвращаемое значение:
//	масЧастей - массив, содержащий части полного имени
//
Функция ИзПолногоИмениПоляПолучитьЧасти(ПолноеИмя)

	масЧастей = Новый Массив;
	СтрИмя = ПолноеИмя;
	
	Пока Не ПустаяСтрока(СтрИмя) Цикл
		Если Лев(СтрИмя, 1) = "[" Тогда
			
			Поз = Найти(СтрИмя, "]");
			Если Поз = 0 Тогда
				масЧастей.Добавить(Сред(СтрИмя, 2));
				СтрИмя = "";
			Иначе
				масЧастей.Добавить(Сред(СтрИмя, 1, Поз));
				СтрИмя = Сред(СтрИмя, Поз + 2);
			КонецЕсли;
			
		Иначе
			
			Поз = Найти(СтрИмя, ".");
			Если Поз = 0 Тогда
				масЧастей.Добавить(СтрИмя);
				СтрИмя = "";
			Иначе
				масЧастей.Добавить(Лев(СтрИмя, Поз - 1));
				СтрИмя = Сред(СтрИмя, Поз + 1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат масЧастей;

КонецФункции // ИзПолногоИмениПоляПолучитьЧасти()

// Находит среди элементов полей СКД поле по имени.
//
// Параметры:
//	Элементы - поля СКД
//	Имя - имя поля СКД
//
// Возвращаемое значение:
//	Поле - поле СКД
//
Функция НайтиПолеСКДПоИмени(Элементы, Имя)
	
	Для Каждого Элемент Из Элементы Цикл
		Если ВРЕГ(Строка(Элемент.Имя)) = ВРЕГ(Имя) Тогда
			Возврат Элемент;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции // НайтиПолеСКДПоИмени()

#КонецОбласти

